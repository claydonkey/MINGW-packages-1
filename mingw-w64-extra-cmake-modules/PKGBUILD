# $Id$
# Maintainer (ArchLinux): Andrea Scarpino <andrea@archlinux.org>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

_realname=extra-cmake-modules
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.26.0
pkgrel=1
pkgdesc='Extra CMake modules (mingw-w64)'
arch=('any')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-cmake"
         "${MINGW_PACKAGE_PREFIX}-png2ico")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-sphinx") # qt5-tools for QtHelp pages
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_realname}-${pkgver}.tar.xz")
sha256sums=('cd529cc10cc4a4fc20a962329ffc8cc93cc200b7dc681aa4ddfc9e9cc88f79ec')

prepare() {
  mkdir -p build-${CARCH}
}

build() {
  mkdir -p build-${CARCH}
  cd build-${CARCH}
  MSYS2_ARG_CONV_EXCL=-DCMAKE_INSTALL_PREFIX= \
    cmake ../${_realname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_HTML_DOCS=OFF \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
